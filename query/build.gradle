apply plugin: 'com.android.feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debugKey {
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('../key/debug.keystore')
        }
    }

    buildTypes {
        debug {
            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            useProguard true
            versionNameSuffix '-debug'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugKey
        }

        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugKey
        }
    }

    productFlavors {
        flavorDimensions 'environment'

        develop {
            dimension 'environment'
        }

        production {
            dimension 'environment'
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

dependencies {
    implementation project(':base')

    implementation deps.support.cardView
    implementation deps.support.design
    implementation deps.mpAndroidChart

    // Dagger 2
    kapt deps.dagger.compiler

    // Permissions Dispatcher
    implementation(deps.permissionsDispatcher.runtime) {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    kapt deps.permissionsDispatcher.compiler

    // Tests
    testImplementation deps.test.junit
    testImplementation deps.test.mockito
    testImplementation deps.test.mockitoKotlin
}
